plugins {
    id 'org.dm.bundle' version '0.10.0'
}
apply plugin: 'java'
apply plugin: "maven"
apply plugin: 'application'
apply plugin: "signing"

group = 'org.daisy.dotify'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main {	java { srcDir 'src' }	resources { srcDir 'src' } }
	test {	java { srcDir 'test' }	resources { srcDir 'test' } }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

def repoRevision = System.getenv("REPO_REVISION")!=null?System.getenv("REPO_REVISION"):repositoryRevision

javadoc {
	options.encoding = 'UTF-8'
	options.links('http://docs.oracle.com/javase/8/docs/api/')
}


mainClassName = "org.daisy.dotify.devtools.cli.Main"
startScripts.applicationName = 'dotifydevtools'

repositories {
    mavenCentral()
    //mavenLocal()
}

dependencies {
    compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
    compile 'org.daisy.braille:braille-utils.api:4.2.1'
    compile 'org.daisy.braille:braille-utils.impl:4.0.1'
    compile 'org.daisy.braille:braille-utils.pef-tools:3.1.0'
    compile 'org.daisy.dotify:dotify.common:4.1.0'

    testCompile group: "junit", name: "junit", version: "4.7"
}

bundle {
	instructions << [
		'-sources': 'true',
		'Export-Package':	'org.daisy.dotify.devtools.cli,'+
							'org.daisy.dotify.devtools.converters,'+
							'org.daisy.dotify.devtools.jvm,'+
							'org.daisy.dotify.devtools.regression,'+
							'org.daisy.dotify.devtools.schematron,'+
							'org.daisy.dotify.devtools.unbrailler',
		'Built-By': System.getProperty("user.name"),
        'Built-On': new Date().format('yyyy-MM-dd'),
        'Repository-Revision': "$repoRevision",
        'Repository-URL': "$repositoryURL",
        'Main-Class': mainClassName,
        'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')	
	]
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
	//Only upload if a git hash is supplied. On Travis only upload snapshots.
	enabled = repoRevision.size()==40 && System.getenv("TRAVIS_BUILD_ID")!=null
    repositories {
        mavenDeployer {
            if (isReleaseVersion) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name 'dotify.devtools'
                packaging 'jar'
                description 'Dotify Devtools'
                url "$repositoryURL"
                scm {
                    connection "$repositorySCM"
                    developerConnection "$repositorySCM"
                    url "$repositoryURL"
                }
                licenses {
                    license {
                        name 'LGPL'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                    }
                }
                developers {
                    developer {
                        id 'joel'
                        name 'Joel HÃ¥kansson'
                    }
                }
            }
        }
    }
}
wrapper {
	gradleVersion = '4.5.1'
	distributionType = Wrapper.DistributionType.ALL
}